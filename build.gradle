import java.text.SimpleDateFormat

/*
 * Data Repository Building Notes
 *
 * USING SWAGGER-CODEGEN
 * We use swagger-codegen to generate our model classes and controller interfaces with default methods.
 * The swagger YAML file is the primary implementation and documentation of the Data Repository API.
 *
 * The gradle technique for running codegen as part of the build is written up here:
 *   https://blog.jcore.com/2017/05/22/automatically-generating-api-using-swagger-and-gradle/
 * It relies on the behavior of the Java plugin, described here:
 *   https://docs.gradle.org/current/userguide/java_plugin.html#sec:java_source_sets
 */

buildscript {
    ext {
        springBootVersion = '2.5.12'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath('io.swagger:swagger-codegen:2.4.27')
    }
}

plugins {
    id "com.google.cloud.tools.jib" version "3.2.0"
    id 'org.liquibase.gradle' version '2.1.1'
    id "org.gradle.test-retry" version "1.4.0"
    id 'antlr'
    id 'com.github.spotbugs' version '4.7.1'
    id "org.hidetake.swagger.generator" version "2.18.2"
    id "org.springframework.boot" version "2.5.12"
    id "idea"
    id "java"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "jacoco"
    id "com.diffplug.spotless" version "6.7.1"
    id "com.dorongold.task-tree" version "2.1.0"
}

allprojects {
    group 'bio.terra'
    version '1.416.0-SNAPSHOT'

    ext {
        resourceDir = "${rootDir}/src/main/resources/api"
    }

    // specifying versions for dependencies, not adding the dependencies themselves (yet)
    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
        dependencies {
            dependency group: "io.swagger.core.v3", name: "swagger-annotations", version: "2.1.5"
            dependency group: "io.swagger.codegen.v3", name: "swagger-codegen-cli", version: "3.0.34"
        }
    }
}

apply from: 'debug.gradle'

// skip subproject tasks by default when building jade-data-repo
subprojects.each { s ->
    s.tasks.all {
        task -> task.enabled = (System.getenv("ENABLE_SUBPROJECT_TASKS") != null)
    }
}

def boolean isCiServer = System.getenv().containsKey("CI")

java {
    sourceCompatibility = 17
    targetCompatibility = 17
}

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
        uploadInBackground = false
        publishAlways()
        def project = ""; def cluster = ""; def nschunk = "";
        if (System.getenv("NAMESPACEINUSE")) {
            project = "broad-jade-integration"
            cluster = "integration-master"
            def ns = System.getenv("NAMESPACEINUSE")
            nschunk = "%0Aresource.labels.namespace_name%3D%22${ns}%22%0Alabels.k8s-pod%2Fcomponent%3D%22${ns}-jade-datarepo-api%22"
        } else {
            project = System.getenv("GOOGLE_CLOUD_PROJECT")
            cluster = "jade-master-us-central1"
            nschunk = ""
        }
        def startTest = new Date().format("yyyy-MM-dd'T'HH:mm:ss'Z'", TimeZone.getTimeZone("UTC"))
        buildFinished { ->
            def endTest = new Date().format("yyyy-MM-dd'T'HH:mm:ss'Z'", TimeZone.getTimeZone("UTC"))
            link "Jump to StackDriver Logs ðŸ”— ", "https://console.cloud.google.com/logs/viewer?interval=CUSTOM&project=${project}&folder&organizationId&minLogLevel=0&expandAll=false&customFacets=&limitCustomFacetWidth=true&scrollTimestamp=${startTest}&dateRangeStart=${startTest}&dateRangeEnd=${endTest}&advancedFilter=resource.type%3D%22k8s_container%22%0Aresource.labels.project_id%3D%22${project}%22%0Aresource.labels.location%3D%22us-central1%22%0Aresource.labels.cluster_name%3D%22${cluster}%22${nschunk}"
        }
    }
}

spotbugs {
    effort = 'max'
    // This makes the "html" reports come out in plain text so you can just open the file in IntelliJ
    // and look at your bugs instead of having to switch to a browser.
    extraArgs = ['-emacs']
}
spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main.html")
            excludeFilter = file("config/spotbugs/excludeFilter.xml")
        }
    }
}
spotbugsTest {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/test.html")
            excludeFilter = file("config/spotbugs/excludeFilter.xml")
        }
    }
}

tasks.withType(JacocoReport) {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['bio/terra/grammar/**'])
        }))
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.1
            }
        }
    }
}
check.dependsOn jacocoTestCoverageVerification
check.dependsOn jacocoTestReport

repositories {
    mavenCentral()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-release-local/'
    }
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
    }
}

configurations {
    generatedCompile
    runtimeClasspath
}

// Fix for CVE-2021-45046
// See https://spring.io/blog/2021/12/10/log4j2-vulnerability-and-spring-boot
ext['log4j2.version'] = '2.17.0'

dependencies {
    // TODO: Unpack the "starter" and include just what we use
    implementation 'com.google.apis:google-api-services-serviceusage:v1-rev20201021-1.30.10'
    implementation 'com.google.apis:google-api-services-appengine:v1-rev20201201-1.31.0'
    implementation 'com.google.apis:google-api-services-oauth2:v2-rev20200213-1.32.1'
    implementation 'com.google.apis:google-api-services-iam:v1-rev20220428-1.32.1'
    implementation platform('com.google.cloud:libraries-bom:25.3.0')
    implementation 'com.google.cloud:google-cloud-billing'
    implementation 'com.google.cloud:google-cloud-resourcemanager'
    implementation 'com.google.cloud:google-cloud-bigquery'
    implementation 'com.google.cloud:google-cloud-storage'
    implementation 'com.google.cloud:google-cloud-firestore'
    implementation 'com.google.cloud:google-cloud-pubsub'
    implementation 'com.google.http-client:google-http-client'
    implementation 'org.apache.commons:commons-dbcp2:2.7.0'     // For database connection support
    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    implementation 'org.apache.directory.studio:org.apache.commons.io:2.4'
    implementation 'org.liquibase:liquibase-core:3.8.0'         // For upgrade
    implementation 'org.postgresql:postgresql:42.2.18'          // Postgres jdbc driver
    implementation 'org.slf4j:slf4j-api:1.7.28'                 // Logging facade
    implementation 'org.codehaus.janino:janino:3.1.6'           // Provides if/else xml parsing for logback config
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation group: "javax.validation", name: "validation-api"

    implementation group: "io.swagger.core.v3", name: "swagger-annotations"
    swaggerCodegen group: "io.swagger.codegen.v3", name: "swagger-codegen-cli"

    implementation 'org.springframework:spring-jdbc:5.1.9.RELEASE'
    implementation 'org.springframework.cloud:spring-cloud-gcp-starter-logging:1.2.8.RELEASE'
    implementation "org.springframework.cloud:spring-cloud-starter-sleuth:3.0.2"
    implementation 'org.broadinstitute.dsde.workbench:sam-client_2.13:0.1-c53306a-SNAP'
    implementation 'org.antlr:ST4:4.3'                          // String templating
    implementation group: 'io.kubernetes', name: 'client-java', version: '10.0.0'

    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'javax.servlet:jstl:1.2'

    implementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client:7.10.0'
    implementation 'org.elasticsearch:elasticsearch:7.10.0'

    implementation group: 'com.microsoft.sqlserver', name: 'mssql-jdbc', version: '9.2.1.jre11'

    // This allows you to build with a local Stairway JAR file, instead of the one from the repository
    // For example, ./gradlew -Pstairwayjar=/Users/dd/code/stairway/build/libs/stairway-0.0.4-SNAPSHOT.jar
    // Or define the environment variable ORG_GRADLE_PROJECT_stairwayjar
    if (project.hasProperty("stairwayjar")) {
        implementation files(project.ext.get("stairwayjar"))
        implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
        implementation group: 'org.openapitools', name: 'jackson-databind-nullable', version: '0.2.2'
    } else {
        implementation 'bio.terra:stairway:0.0.75-SNAPSHOT'
    }

    // Similar development mode for pointing to terra common library
    if (project.hasProperty("commonlibjar")) {
        implementation files(project.ext.get("commonlibjar"))
        implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
        implementation group: 'org.openapitools', name: 'jackson-databind-nullable', version: '0.2.2'
    } else {
        implementation ('bio.terra:terra-common-lib:0.0.55-SNAPSHOT') {
            exclude group: 'org.broadinstitute.dsde.workbench', module: 'sam-client_2.12'
        }
    }

    implementation group: "bio.terra", name: "terra-resource-buffer-client", version: "0.4.3-SNAPSHOT"
    implementation group: "bio.terra", name: "externalcreds-client-resttemplate", version: "0.72.0-SNAPSHOT"
    implementation group: "org.glassfish.jersey.inject", name: "jersey-hk2", version: "2.30.1"

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.8.6'

    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.2'

    // Azure related dependencies
    implementation 'com.azure:azure-identity:1.2.5'
    implementation 'com.azure.resourcemanager:azure-resourcemanager:2.4.0'
    implementation 'com.azure:azure-storage-common:12.11.1'
    implementation 'com.azure:azure-storage-file-datalake:12.5.1'
    implementation 'com.azure:azure-data-tables:12.1.0'

    testImplementation "org.apache.parquet:parquet-common:1.12.0"
    testImplementation "org.apache.parquet:parquet-hadoop:1.12.0"
    testImplementation "org.apache.parquet:parquet-hadoop-bundle:1.12.0"
    testImplementation "org.apache.parquet:parquet-encoding:1.12.0"
    testImplementation "org.apache.parquet:parquet-column:1.12.0"
    testImplementation ('org.apache.hadoop:hadoop-common:3.3.1')  {
        exclude group: 'com.sun.jersey', module: 'jersey-core'
        exclude group: 'com.sun.jersey', module: 'jersey-servlet'
        exclude group: 'com.sun.jersey', module: 'jersey-json'
        exclude group: 'com.sun.jersey', module: 'jersey-server'
    }
    testImplementation ('org.apache.hadoop:hadoop-azure:3.3.1') {
        exclude group: 'com.sun.jersey', module: 'jersey-core'
        exclude group: 'com.sun.jersey', module: 'jersey-servlet'
        exclude group: 'com.sun.jersey', module: 'jersey-json'
        exclude group: 'com.sun.jersey', module: 'jersey-server'
    }
    testImplementation('org.apache.hadoop:hadoop-mapreduce-client-core:3.3.1') {
        exclude group: 'com.sun.jersey', module: 'jersey-core'
        exclude group: 'com.sun.jersey', module: 'jersey-servlet'
        exclude group: 'com.sun.jersey', module: 'jersey-json'
        exclude group: 'com.sun.jersey', module: 'jersey-server'
    }


    antlr "org.antlr:antlr4:4.8"
    spotbugs 'com.github.spotbugs:spotbugs:4.2.3'

    // Need groovy on the class path for the logback config. Could use XML and skip this dependency,
    // but the groovy config is... well... groovy.
    runtimeOnly group: 'org.codehaus.groovy', name: 'groovy', version: '3.0.7'

    // Findbugs annotations, so we can selectively suppress findbugs findings
    compileOnly 'com.google.code.findbugs:annotations:3.0.1'

    liquibaseRuntime 'org.liquibase:liquibase-core:3.8.0'
    liquibaseRuntime 'org.postgresql:postgresql:42.2.7'
    liquibaseRuntime 'ch.qos.logback:logback-classic:1.2.3'

    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.1'
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation 'org.postgresql:postgresql:42.2.8'
    testImplementation 'com.google.code.findbugs:annotations:3.0.1'
    testImplementation 'io.zonky.test:embedded-database-spring-test:2.1.1'
    testImplementation 'io.zonky.test:embedded-postgres:1.3.1'
    implementation enforcedPlatform('io.zonky.test.postgres:embedded-postgres-binaries-bom:12.8.0')

    testImplementation 'org.awaitility:awaitility:4.0.3'

    generatedCompile 'org.springframework.boot:spring-boot-starter-web'
    // boot-starter-validation required for javax.validation.Valid references, @Valid tags
    generatedCompile 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation group: "org.webjars", name: "webjars-locator-core", version: "0.46"
    runtimeOnly group: "org.webjars.npm", name: "swagger-ui-dist", version: "4.3.0"
    generatedCompile group: "io.swagger.core.v3", name: "swagger-annotations"

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    constraints {
        implementation('org.bouncycastle:bcpg-jdk15on:1.68') {
            because '1.66 has security vulnerabilities'
        }
        implementation('org.bouncycastle:bcpkix-jdk15on:1.68') {
            because '1.66 has security vulnerabilities'
        }
        implementation('org.bouncycastle:bcprov-ext-jdk15on:1.68') {
            because '1.66 has security vulnerabilities'
        }
        implementation('org.bouncycastle:bcprov-jdk15on:1.68') {
            because '1.66 has security vulnerabilities'
        }
    }
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

swaggerSources {
    server {
        inputFile = file("${resourceDir}/data-repository-openapi.yaml")
        code {
            language = "spring"
            library = "spring-boot"
            outputDir = file("${buildDir}/generated-src/swagger")
            components = ["models", "apis"]
            rawOptions = [
                "--invoker-package", "bio.terra",
                "--api-package", "bio.terra.controller",
                "--model-package", "bio.terra.model",
                "-D", "interfaceOnly=true," +
                    "useTags=true," +
                    "dateLibrary=java8"
            ]
        }
    }
}

task generateVersionFile {
    def properties = new Properties()
    def versionFile = file "src/main/resources/version.properties"
    properties.setProperty("semVer", project.version)
    properties.setProperty("gitHash", getGitHash())
    Writer writer = new FileWriter(versionFile, false)
    properties.store(writer, "DO NOT EDIT. Autogenerated by build.gradle.")
    writer.close()
}

generateGrammarSource {
    arguments += ["-visitor", "-long-messages"]
}

task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}

def generatedOutputDir = 'src/generated/java'
clean.doFirst {
    delete("${projectDir}/$generatedOutputDir")
    delete("${rootDir}/datarepo-client/build")
    delete("${rootDir}/datarepo-client/generated")
}

sourceSets {
    generated {
        compileClasspath = configurations.generatedCompile
    }
    main {
        compileClasspath += generated.output
        runtimeClasspath += generated.output
    }
    test {
        compileClasspath += generated.output
        runtimeClasspath += generated.output
    }
    jacoco {
        toolVersion '0.8.7'
    }
}

sourceSets.main.java.srcDir "${buildDir}/generated-src/swagger"

bootRun {
    classpath += sourceSets.generated.output
}

jar {
    from sourceSets.generated.output
}

jib {
    extraDirectories {
        mkdir 'build/gen-expanded'
        paths = ['build/gen-expanded']
    }
    from {
        image = "us.gcr.io/broad-dsp-gcr-public/base/jre:17-debian"
    }
    to {
        image = "gcr.io/broad-jade-dev/jade-data-repo:" + (System.env.GCR_TAG ?: getGitHash())
    }
    // Make sure that we disable remote lookups in message printing.  See
    // https://www.randori.com/blog/cve-2021-44228/
    // for more information
    container {
        jvmFlags = ["-Xms1g", "-Xmx2g", "-Dlog4j2.formatMsgNoLookups=true"]
    }
    container.creationTime = buildTime()
}

def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'") // you can change it
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}
// jib expects all classes to be under app/classes in the resulting image. this, combined with the extraDirectories
// setting above will ensure that the generated classes end up in the right place when jib builds.
task setupGeneratedDir(type: Copy) {
    from file('build/classes/java/generated')
    into file('build/gen-expanded/app/classes')
}
tasks.jib.dependsOn setupGeneratedDir

liquibase {
    activities {
        datarepo {
            changeLogFile project.ext.dbDatarepoChangesetFile
            url project.ext.dbDatarepoUri
            username project.ext.dbDatarepoUsername
            password project.ext.dbDatarepoPassword
            logLevel 'info'
        }
        // This is setup only to run "dropAll", not commands that require the changelog, like "update"
        // To setup more functionality, see notes in this doc:
        // https://docs.google.com/document/d/1hxT7ZOAY4MKPhjoLLZNJE4IB_3WncsDDYEg8m1myL-I/edit?usp=sharing
        stairway {
            url project.ext.dbStairwayUri
            username project.ext.dbStairwayUsername
            password project.ext.dbStairwayPassword
            logLevel 'info'
        }
    }
}

spotless {
    java {
        target project.fileTree(project.rootDir) {
            include 'src/**/*.java'
        }
        googleJavaFormat()
    }
}

// -- Test tasks --
// The default 'test' task runs all of the unit tests.
// There are separate tasks for running just the unit tests and just the connected tests.
// connected tests and running all of the tests.

tasks.withType(Test).configureEach {
    systemProperty 'spring.profiles.include', 'human-readable-logging'
}

test {
    useJUnit {
        includeCategories 'bio.terra.common.category.Unit'
    }
    outputs.upToDateWhen { false }
    maxParallelForks = 2
}

task testConnected(type: Test) {
    useJUnit {
        includeCategories 'bio.terra.common.category.Connected'
    }
    testLogging {
        events = ["passed", "failed", "skipped", "started", "standard_out"]
    }
    retry {
        if (isCiServer) {
            maxRetries = 3
            maxFailures = 15
        } else {
            maxRetries = 2
        }
    }
    maxParallelForks = 4
    outputs.upToDateWhen { false }
    maxHeapSize = "2g"
}

task testIntegration(type: Test) {
    useJUnit {
        includeCategories 'bio.terra.common.category.Integration'
    }
    testLogging {
        events = ["passed", "failed", "skipped", "started", "standard_out"]
    }

    retry {
        if (isCiServer) {
            maxRetries = 3
            maxFailures = 15
        } else {
            maxRetries = 2
        }
    }
    maxParallelForks = 8
    outputs.upToDateWhen { false }
}

task testUnit(type: Test) {
    useJUnit {
        includeCategories 'bio.terra.common.category.Unit'
    }
    maxParallelForks = 2
    outputs.upToDateWhen { false }
}

task testOnDemand(type: Test) {
    useJUnit {
        includeCategories 'bio.terra.common.category.OnDemand'
    }
    testLogging {
        events = ["passed", "failed", "skipped", "started", "standard_out"]
    }
    outputs.upToDateWhen { false }
}

task testAll(type: Test) {
    useJUnit {
        // Note: explicitly not including OnDemand tests
        includeCategories 'bio.terra.common.category.Connected', 'bio.terra.common.category.Unit'
    }
    outputs.upToDateWhen { false }
}

compileGeneratedJava.dependsOn swaggerSources.server.code
compileGeneratedJava.dependsOn generateGrammarSource
classes.dependsOn generatedClasses
compileJava.dependsOn compileGeneratedJava
ideaModule.dependsOn swaggerSources.server.code
idea.module.sourceDirs += file("$buildDir/generated-src/swagger/src/main/java")

// Run spotless check when running in github actions, otherwise run spotless apply.
compileJava {
    if (isCiServer) {
        dependsOn(spotlessCheck)
    } else {
        dependsOn(spotlessApply)
    }
}
