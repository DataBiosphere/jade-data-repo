plugins {
    id 'java'
    id 'application'
    id "com.google.cloud.tools.jib" version "1.6.1"
    id "com.diffplug.spotless" version "5.14.0"
    id 'com.github.spotbugs' version '4.7.1'
}

repositories {
    mavenCentral()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-release-local/'
    }
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
    }
}

dependencies {
    ext {
        junit = "5.4.2"
        findbugsAnnotations = "3.0.1"

        jackson = "2.10.2"
        kubernetesClient = "10.0.0"
        logback = "1.2.3"
        slf4j = "1.7.25"
        hamcrest = "2.1"
        apacheMath = "3.0"

        googleApi = "1.23.0"
        googleCloud = "1.108.0"
        googlePeople = "v1-rev277-1.23.0"
        googleOauth2 = "0.20.0"
        googleMonitoring = "2.0.1"
        googleLogging = "1.101.2"

        swaggerAnnotations = "2.1.5"
        jersey = "2.30.1"

        datarepoClient = "1.110.0-SNAPSHOT"
        samClient = "0.1-9435410-SNAP"
    }

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junit}")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:${junit}")
    compileOnly "com.google.code.findbugs:annotations:${findbugsAnnotations}"

    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jackson}"
    compile "io.kubernetes:client-java:${kubernetesClient}"
    compile "ch.qos.logback:logback-classic:${logback}"
    compile "org.slf4j:slf4j-api:${slf4j}"
    compile "org.hamcrest:hamcrest:${hamcrest}"
    compile "org.apache.commons:commons-math3:${apacheMath}"

    compile "com.google.api-client:google-api-client:${googleApi}"
    compile "com.google.oauth-client:google-oauth-client-jetty:${googleApi}"
    compile "com.google.apis:google-api-services-people:${googlePeople}"
    compile "com.google.auth:google-auth-library-oauth2-http:${googleOauth2}"
    compile "com.google.cloud:google-cloud-bigquery:${googleCloud}"
    compile "com.google.cloud:google-cloud-storage:${googleCloud}"
    compile "com.google.cloud:google-cloud-monitoring:${googleMonitoring}"
    compile "com.google.cloud:google-cloud-logging:${googleLogging}"

    implementation "org.glassfish.jersey.core:jersey-client:${jersey}"
    implementation "org.glassfish.jersey.media:jersey-media-json-jackson:${jersey}"
    implementation "org.glassfish.jersey.media:jersey-media-multipart:${jersey}"
    implementation "org.glassfish.jersey.inject:jersey-hk2:${jersey}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jackson}"

    // Gradle project property "datarepoclientjar" overrides the fetch from Maven
    if (project.hasProperty("datarepoclientjar")) {
        implementation files(project.findProperty("datarepoclientjar"))
    } else {
        implementation "bio.terra:datarepo-client:${datarepoClient}"
        compile "io.swagger.core.v3:swagger-annotations:${swaggerAnnotations}"
    }

    implementation "org.broadinstitute.dsde.workbench:sam-client_2.12:${samClient}"
}

group 'bio.terra'
version '1.0-SNAPSHOT'
sourceCompatibility = 11
targetCompatibility = 11

test {
    useJUnitPlatform()

    testLogging {
        showStandardStreams = true
    }
}

application {
    mainClassName = 'common.commands.PrintHelp'
}

task(runTest, dependsOn: 'classes', type: JavaExec) {
    main = "common.commands.RunTest"
    classpath = sourceSets.main.runtimeClasspath
}
task(lockNamespace, dependsOn: 'classes', type: JavaExec) {
    main = "common.commands.LockNamespace"
    classpath = sourceSets.main.runtimeClasspath
}
task(unlockNamespace, dependsOn: 'classes', type: JavaExec) {
    main = "common.commands.UnlockNamespace"
    classpath = sourceSets.main.runtimeClasspath
}
task(lockAndRunTest, dependsOn: 'classes', type: JavaExec) {
    main = "common.commands.LockAndRunTest"
    classpath = sourceSets.main.runtimeClasspath
}
task(collectMeasurements, dependsOn: 'classes', type: JavaExec) {
    main = "common.commands.CollectMeasurements"
    classpath = sourceSets.main.runtimeClasspath
}
task(uploadResults, dependsOn: 'classes', type: JavaExec) {
    main = "common.commands.UploadResults"
    classpath = sourceSets.main.runtimeClasspath
}
task(printHelp, dependsOn: 'classes', type: JavaExec) {
    main = "common.commands.PrintHelp"
    classpath = sourceSets.main.runtimeClasspath
}

spotless {
    java {
        googleJavaFormat()
    }
}

spotbugs {
    effort = 'max'
    // This makes the "html" reports come out in plain text so you can just open the file in IntelliJ
    // and look at your bugs instead of having to switch to a browser.
    extraArgs = [ '-emacs']
}
spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main.txt")
            stylesheet = 'fancy.xsl'
        }
    }
}
spotbugsTest {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/test.txt")
            stylesheet = 'fancy.xsl'
        }
    }
}

compileJava.dependsOn tasks.spotlessApply
