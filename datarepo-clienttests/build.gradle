plugins {
    id 'java'
    id 'application'
    id "com.google.cloud.tools.jib" version "1.6.1"
    id "com.diffplug.spotless" version "6.7.1"
    id 'com.github.spotbugs' version '4.7.1'
}

repositories {
    mavenCentral()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-release-local/'
    }
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
    }
}

dependencies {
    ext {
        junit = "5.4.2"
        findbugsAnnotations = "3.0.1"

        jackson = "2.10.2"
        kubernetesClient = "10.0.0"
        logback = "1.2.3"
        slf4j = "1.7.25"
        hamcrest = "2.1"
        apacheMath = "3.0"

        googleApi = "1.23.0"
        googlePeople = "v1-rev277-1.23.0"
        googleOauth2 = "0.20.0"

        swaggerAnnotations = "2.1.5"
        jersey = "2.32"

        datarepoClient = "1.343.0-SNAPSHOT"
        samClient = "0.1-c53306a-SNAP"
    }

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junit}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junit}")
    compileOnly "com.google.code.findbugs:annotations:${findbugsAnnotations}"

    implementation "org.awaitility:awaitility:4.0.3"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jackson}"
    implementation "io.kubernetes:client-java:${kubernetesClient}"
    implementation "ch.qos.logback:logback-classic:${logback}"
    implementation "org.slf4j:slf4j-api:${slf4j}"
    implementation "org.hamcrest:hamcrest:${hamcrest}"
    implementation "org.apache.commons:commons-math3:${apacheMath}"

    implementation "com.google.api-client:google-api-client:${googleApi}"
    implementation "com.google.oauth-client:google-oauth-client-jetty:${googleApi}"
    implementation "com.google.apis:google-api-services-people:${googlePeople}"
    implementation "com.google.auth:google-auth-library-oauth2-http:${googleOauth2}"
    implementation platform('com.google.cloud:libraries-bom:25.3.0')
    implementation "com.google.cloud:google-cloud-bigquery"
    implementation "com.google.cloud:google-cloud-storage"
    implementation "com.google.cloud:google-cloud-monitoring"
    implementation "com.google.cloud:google-cloud-logging"

    implementation "org.glassfish.jersey.core:jersey-client:${jersey}"
    implementation "org.glassfish.jersey.media:jersey-media-json-jackson:${jersey}"
    implementation "org.glassfish.jersey.media:jersey-media-multipart:${jersey}"
    implementation "org.glassfish.jersey.inject:jersey-hk2:${jersey}"
    implementation "org.glassfish.jersey.connectors:jersey-jdk-connector:${jersey}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jackson}"

    // Azure related dependencies
    implementation 'com.azure:azure-identity:1.2.5'
    implementation 'com.azure.resourcemanager:azure-resourcemanager:2.4.0'
    implementation 'com.azure:azure-storage-common:12.11.1'
    implementation 'com.azure:azure-storage-file-datalake:12.5.1'
    implementation 'com.azure:azure-data-tables:12.1.0'

    // Gradle project property "datarepoclientjar" overrides the fetch from Maven
    if (project.hasProperty("datarepoclientjar")) {
        implementation files(project.findProperty("datarepoclientjar"))
    } else {
        implementation "bio.terra:datarepo-client:${datarepoClient}"
        implementation "io.swagger.core.v3:swagger-annotations:${swaggerAnnotations}"
    }

    implementation "org.broadinstitute.dsde.workbench:sam-client_2.13:${samClient}"
}

group 'bio.terra'
version '1.0-SNAPSHOT'

java {
    sourceCompatibility = 17
    targetCompatibility = 17
}

test {
    useJUnitPlatform()

    testLogging {
        showStandardStreams = true
    }
}

application {
    mainClassName = 'common.commands.PrintHelp'
}

task(runTest, dependsOn: 'classes', type: JavaExec) {
    main = "common.commands.RunTest"
    classpath = sourceSets.main.runtimeClasspath
}
task(lockNamespace, dependsOn: 'classes', type: JavaExec) {
    main = "common.commands.LockNamespace"
    classpath = sourceSets.main.runtimeClasspath
}
task(unlockNamespace, dependsOn: 'classes', type: JavaExec) {
    main = "common.commands.UnlockNamespace"
    classpath = sourceSets.main.runtimeClasspath
}
task(lockAndRunTest, dependsOn: 'classes', type: JavaExec) {
    main = "common.commands.LockAndRunTest"
    classpath = sourceSets.main.runtimeClasspath
}
task(collectMeasurements, dependsOn: 'classes', type: JavaExec) {
    main = "common.commands.CollectMeasurements"
    classpath = sourceSets.main.runtimeClasspath
}
task(uploadResults, dependsOn: 'classes', type: JavaExec) {
    main = "common.commands.UploadResults"
    classpath = sourceSets.main.runtimeClasspath
}
task(printHelp, dependsOn: 'classes', type: JavaExec) {
    main = "common.commands.PrintHelp"
    classpath = sourceSets.main.runtimeClasspath
}

spotless {
    java {
        googleJavaFormat()
    }
}

spotbugs {
    effort = 'max'
    // This makes the "html" reports come out in plain text so you can just open the file in IntelliJ
    // and look at your bugs instead of having to switch to a browser.
    extraArgs = [ '-emacs']
}
spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main.txt")
            stylesheet = 'fancy.xsl'
        }
    }
}
spotbugsTest {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/test.txt")
            stylesheet = 'fancy.xsl'
        }
    }
}

compileJava.dependsOn tasks.spotlessApply
